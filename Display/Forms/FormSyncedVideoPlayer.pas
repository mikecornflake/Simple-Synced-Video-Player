Unit FormSyncedVideoPlayer;

{$mode objfpc}{$H+}
{$WARN 5024 off : Parameter "$1" not used}

Interface

Uses
  Classes, SysUtils, FileUtil, Forms, Controls, Graphics, Dialogs, ExtCtrls,
  Buttons, Menus, ActnList, ComCtrls, FrameVideoPlayer, FrameSyncedVideo,
  FormMain, IniFiles, MRUs;

Type

  { TfrmSyncedVideoPlayer }

  TfrmSyncedVideoPlayer = Class(TFormMain)
    lvFiles: TListView;
    mnuToggleVideo: TMenuItem;
    mnuView: TMenuItem;
    Separator1: TMenuItem;
    mnuOpenRecent: TMenuItem;
    mnuExit: TMenuItem;
    mnuFile: TMenuItem;
    mnuOpen: TMenuItem;
    dlgOpen: TOpenDialog;
    pnlVideoPlayer: TPanel;
    Splitter1: TSplitter;
    tmrUpdate: TTimer;
    Procedure FormActivate(Sender: TObject);
    Procedure FormClose(Sender: TObject; Var CloseAction: TCloseAction);
    Procedure FormCreate(Sender: TObject);
    Procedure FormDestroy(Sender: TObject);
    Procedure FormDropFiles(Sender: TObject; Const FileNames: Array Of String);
    Procedure lvFilesSelectItem(Sender: TObject; Item: TListItem; Selected: Boolean);
    Procedure mnuExitClick(Sender: TObject);
    Procedure mnuFileClick(Sender: TObject);
    Procedure mnuOpenClick(Sender: TObject);
    Procedure mnuOpenRecentClick(Sender: TObject);
    Procedure mnuToggleVideoClick(Sender: TObject);
    Procedure tmrUpdateTimer(Sender: TObject);
  Private
    fmeVideoPlayer: TFrameVideoPlayer;
    fmeSyncedVideo: TFrameSyncedVideo;
    FMRU: TMRU;
    FLoaded: Boolean;
    FInternalLoad: Boolean;
    FIgnoreListViewSelectItem: Integer;
    FFolder: String;

    Procedure OpenVideo(Const AFiles: TStrings); Overload;
    Procedure OpenVideo(Const AFiles: TStringArray); Overload;
    Procedure ParseFolder(AFile: String);
  Public
    // Stored in ini file with exe - what folders to load etc
    Procedure LoadGlobalSettings(oInifile: TIniFile); Override;
    Procedure SaveGlobalSettings(oInifile: TIniFile); Override;
  End;

Var
  frmSyncedVideoPlayer: TfrmSyncedVideoPlayer;

Implementation

Uses
  FileSupport, VideoEngineFactory, ControlGridLayout, StringSupport,
  InspectionSupport, DateUtils,

  // Include all required video playback engines below this point
  FrameVideoLibmpv;

  {$R *.lfm}

  { TfrmSyncedVideoPlayer }

Procedure TfrmSyncedVideoPlayer.FormCreate(Sender: TObject);
Begin
  Inherited;

  fmeVideoPlayer := TFrameVideoPlayer.Create(Self);
  fmeVideoPlayer.Parent := pnlVideoPlayer;
  fmeVideoPlayer.Name := 'fmeVideoPlayer';
  fmeVideoPlayer.Align := alClient;
  fmeVideoPlayer.Autoplay := True;
  fmeVideoPlayer.ShowLabel := True;

  // Change this line to swap playback engines.
  fmeVideoPlayer.VideoEngineClass := TFrameSyncedVideo;

  fmeSyncedVideo := nil;

  If assigned(fmeVideoPlayer.PlaybackFrame) Then
  Begin
    If fmeVideoPlayer.PlaybackFrame Is TFrameSyncedVideo Then
    Begin
      fmeSyncedVideo := TFrameSyncedVideo(fmeVideoPlayer.PlaybackFrame);
      fmeSyncedVideo.VideoEngineClass := TVideoEngineFactory.DefaultClass;
    End;
  End;

  // Disable require --configure
  FAlwaysSaveSettings := True;

  FMRU := TMRU.Create;
  FMRU.Max := 10;
  FMRU.Files := True;

  FLoaded := False;
  FInternalLoad := False;
  FIgnoreListViewSelectItem := 0;
  FFolder := '';

  Caption := Application.Title;

  sbMain.Panels[0].Text := '';
  sbMain.Panels[1].Text := 'Start:';
  sbMain.Panels[2].Text := 'Duration:';
  sbMain.Panels[3].Text := 'End:';
End;

Procedure TfrmSyncedVideoPlayer.FormActivate(Sender: TObject);
Var
  slFiles: TStringList;
  i: Integer;
  sFile, sExt: String;
Begin
  Inherited;

  If Not FLoaded Then
  Begin
    If Application.ParamCount > 0 Then
    Begin
      slFiles := TStringList.Create;
      Try
        For i := 1 To Application.ParamCount Do
        Begin
          sFile := Application.Params[i];
          sExt := ExtractFileExt(LowerCase(sFile));
          If IsVideo(sExt) Then
            slFiles.Add(sFile);
        End;

        If slFiles.Count > 0 Then
          OpenVideo(slFiles);
      Finally
        slFiles.Free;
      End;
    End;

    FLoaded := True;
  End;
End;

Procedure TfrmSyncedVideoPlayer.FormClose(Sender: TObject; Var CloseAction: TCloseAction);
Begin
  If Assigned(fmeVideoPlayer) Then
    fmeVideoPlayer.Clear;

  Inherited;
End;

Procedure TfrmSyncedVideoPlayer.FormDestroy(Sender: TObject);
Begin
  FreeAndNil(FMRU);

  Inherited;
End;

Const
  RELATED_VIDEO_WINDOW_SEC = 10;

Type
  TVideoFileInfo = Record
    FullName: String;
    FileName: String;
    HasDateTime: Boolean;
    DateTime: TDateTime;
  End;

Function SecondsApart(Const A, B: TDateTime): Double;
Begin
  Result := Abs(A - B) * 24 * 60 * 60;
End;

Function CompareVideoFileInfo(Const A, B: TVideoFileInfo): Integer;
Begin
  If A.HasDateTime And B.HasDateTime Then
  Begin
    If A.DateTime < B.DateTime Then Exit(-1);
    If A.DateTime > B.DateTime Then Exit(1);
    Result := CompareText(A.FileName, B.FileName);
  End
  Else If A.HasDateTime Then
    Result := -1
  Else If B.HasDateTime Then
    Result := 1
  Else
    Result := CompareText(A.FileName, B.FileName);
End;

Procedure SortVideoFiles(Var AFiles: Array Of TVideoFileInfo);

  Procedure QuickSort(L, R: Integer);
  Var
    I, J: Integer;
    Pivot, Temp: TVideoFileInfo;
  Begin
    I := L;
    J := R;
    Pivot := AFiles[(L + R) Div 2];

    Repeat
      While CompareVideoFileInfo(AFiles[I], Pivot) < 0 Do Inc(I);
      While CompareVideoFileInfo(AFiles[J], Pivot) > 0 Do Dec(J);

      If I <= J Then
      Begin
        Temp := AFiles[I];
        AFiles[I] := AFiles[J];
        AFiles[J] := Temp;
        Inc(I);
        Dec(J);
      End;
    Until I > J;

    If L < J Then QuickSort(L, J);
    If I < R Then QuickSort(I, R);
  End;

Begin
  If Length(AFiles) > 1 Then
    QuickSort(0, High(AFiles));
End;

Procedure TfrmSyncedVideoPlayer.ParseFolder(AFile: String);
Var
  sFolder, sExt, sSearchMask, sFullName: String;
  oSearchRec: TSearchRec;
  oParsedInfo: TInspectionFilenameInfo;
  Files: Array Of TVideoFileInfo;
  i, iGroupStart, iCount: Integer;
  oItem, oSelect: TListItem;
  bSelectedInGroup: Boolean;

  Procedure AddFile(Const AFullName, AFileName: String);
  Var
    n: Integer;
  Begin
    n := Length(Files);
    SetLength(Files, n + 1);

    Files[n].FullName := AFullName;
    Files[n].FileName := AFileName;
    Files[n].HasDateTime := TryParseInspectionFilename(AFullName, oParsedInfo);

    If Files[n].HasDateTime Then
      Files[n].DateTime := oParsedInfo.DateTime
    Else
      Files[n].DateTime := 0;
  End;

Begin
  oSelect := nil;
  sFolder := ExtractFileDir(AFile);
  sSearchMask := IncludeTrailingPathDelimiter(sFolder) + '*.*';

  If FindFirst(sSearchMask, faAnyFile And Not faDirectory, oSearchRec) = 0 Then
  Begin
    Try
      Repeat
        sFullName := IncludeTrailingPathDelimiter(sFolder) + oSearchRec.Name;
        sExt := ExtractFileExt(sFullName);

        If IsVideo(sExt) Then
          AddFile(sFullName, oSearchRec.Name);

      Until FindNext(oSearchRec) <> 0;
    Finally
      FindClose(oSearchRec);
    End;
  End;

  SortVideoFiles(Files);

  lvFiles.Items.Clear;
  lvFiles.BeginUpdate;
  Try
    i := 0;

    While i <= High(Files) Do
    Begin
      iGroupStart := i;
      iCount := 1;
      bSelectedInGroup := SameFileName(Files[i].FullName, AFile);

      If Files[i].HasDateTime Then
      Begin
        Inc(i);

        While (i <= High(Files)) And Files[i].HasDateTime And
          (SecondsApart(Files[i].DateTime, Files[iGroupStart].DateTime) <=
            RELATED_VIDEO_WINDOW_SEC) Do
        Begin
          Inc(iCount);

          If SameFileName(Files[i].FullName, AFile) Then
            bSelectedInGroup := True;

          Inc(i);
        End;
      End
      Else
        Inc(i);

      oItem := lvFiles.Items.Add;

      If Files[iGroupStart].HasDateTime Then
      Begin
        oItem.Caption := FormatDateTime('HH:nn:ss', Files[iGroupStart].DateTime);
        oItem.SubItems.Add(FormatDateTime('yyyy-mm-dd', Files[iGroupStart].DateTime));
      End
      Else
      Begin
        oItem.Caption := '';
        oItem.SubItems.Add('');
      End;

      oItem.SubItems.Add(IntToStr(iCount));
      oItem.SubItems.Add(Files[iGroupStart].FileName);

      If bSelectedInGroup Then
        oSelect := oItem;
    End;

  Finally
    lvFiles.EndUpdate;

    If Assigned(oSelect) Then
    Begin
      Inc(FIgnoreListViewSelectItem);
      Try
        oSelect.Selected := True;
        oSelect.Focused := True;
        oSelect.MakeVisible(False);
      Finally
        Dec(FIgnoreListViewSelectItem);
      End;
    End;
  End;
End;

Procedure TfrmSyncedVideoPlayer.FormDropFiles(Sender: TObject; Const FileNames: Array Of String);
Var
  sExt, sFile: String;
  slFiles: TStringList;
Begin
  If Length(FileNames) = 0 Then
    Exit;

  slFiles := TStringList.Create;
  Try
    For sFile In FileNames Do
    Begin
      sExt := ExtractFileExt(LowerCase(sFile));
      If IsVideo(sExt) Then
        slFiles.Add(sFile);
    End;

    OpenVideo(slFiles);
  Finally
    slFiles.Free;
  End;
End;

Procedure TfrmSyncedVideoPlayer.lvFilesSelectItem(Sender: TObject; Item: TListItem;
  Selected: Boolean);
Var
  arrFiles: TStringArray;
  sFile: String;
Begin
  If (FIgnoreListViewSelectItem > 0) Or FInternalLoad Then
    Exit;

  If (Item.Selected) And (lvFiles.Selected = Item) Then
  Begin
    arrFiles := [];
    sFile := IncludeSlash(FFolder) + Item.Subitems[2];
    AddStringToArray(arrFiles, sFile);

    FInternalLoad := True;
    OpenVideo(arrFiles);
    FInternalLoad := False;
  End;
End;

Procedure TfrmSyncedVideoPlayer.OpenVideo(Const AFiles: TStrings);
Var
  arrFiles: TStringArray;
Begin
  arrFiles := AFiles.ToStringArray;
  OpenVideo(arrFiles);
End;

Procedure TfrmSyncedVideoPlayer.OpenVideo(Const AFiles: TStringArray);
Var
  arrFiles: TStringArray;
  sFile, sChannel: String;
  oInspectionFilenameInfo: TInspectionFilenameInfo;
  dtStart, dtEnd: TDateTime;
Begin
  If Not Assigned(AFiles) Then
    Exit;

  If Length(AFiles) = 0 Then
  Begin
    fmeSyncedVideo.Rate := 1.0;
    fmeVideoPlayer.Clear;
  End;

  arrFiles := AFiles;

  If Length(arrFiles) = 1 Then
  Begin
    sFile := arrFiles[0];

    If TryParseInspectionFilename(sFile, oInspectionFilenameInfo) And
      oInspectionFilenameInfo.FoundDateTime Then
    Begin
      // Adjust this window to taste.  Currently +/- 5 seconds
      dtStart := IncSecond(oInspectionFilenameInfo.DateTime, -5);
      dtEnd := IncSecond(dtStart, 10);

      arrFiles := FindFilesStartingInWindow(sFile, dtStart, dtEnd);

      If Length(arrFiles) = 0 Then
      Begin
        SetLength(arrFiles, 1);
        arrFiles[0] := sFile;
      End;
    End;
  End;

  Busy := True;
  BeginFormUpdate;
  Try
    If Not Assigned(fmeSyncedVideo) Then
      If assigned(fmeVideoPlayer.PlaybackFrame) Then
      Begin
        If fmeVideoPlayer.PlaybackFrame Is TFrameSyncedVideo Then
        Begin
          fmeSyncedVideo := TFrameSyncedVideo(fmeVideoPlayer.PlaybackFrame);
          fmeSyncedVideo.VideoEngineClass := TVideoEngineFactory.DefaultClass;
        End;
      End;

    fmeSyncedVideo.BeginLoadVideos;
    Try
      For sFile In arrFiles Do
      Begin
        If FileExists(sFile) And (fmeSyncedVideo.VideoFileCount < 4) Then
        Begin
          TryParseInspectionFilename(sFile, oInspectionFilenameInfo);

          If oInspectionFilenameInfo.FoundChannel Then
            sChannel := oInspectionFilenameInfo.Channel
          Else
            sChannel := '';

          If oInspectionFilenameInfo.FoundDateTime Then
            dtStart := oInspectionFilenameInfo.DateTime
          Else
            dtStart := 0;

          fmeSyncedVideo.Load(sFile, sChannel, dtStart);
          FMRU.Add(sFile);
        End;
      End;

    Finally
      fmeSyncedVideo.EndLoadVideos;
    End;

    If fmeSyncedVideo.VideoFileCount > 0 Then
    Begin
      If fmeSyncedVideo.VideoFileCount > 2 Then
        fmeSyncedVideo.Layout(2, 2, clsLeftToRightThenDown)
      Else
        fmeSyncedVideo.Layout(1, fmeSyncedVideo.VideoFileCount, clsLeftToRightThenDown);

      // Play the video
      fmeSyncedVideo.Play;
      fmeVideoPlayer.RefreshUI;

      FFolder := ExtractFileDir(sFile);

      Caption := Format('%s: %s', [Application.Title, fmeSyncedVideo.Filename]);

      tmrUpdate.Enabled := True;

      If Not FInternalLoad Then
      Begin
        Inc(FIgnoreListViewSelectItem);
        Try
          ParseFolder(fmeSyncedVideo.Filename);
        Finally
          Dec(FIgnoreListViewSelectItem);
        End;
      End;
    End;
  Finally
    EndFormUpdate;
    Busy := False;
  End;
End;

Procedure TfrmSyncedVideoPlayer.mnuExitClick(Sender: TObject);
Begin
  Close;
End;

Procedure TfrmSyncedVideoPlayer.mnuFileClick(Sender: TObject);
Begin
  FMRU.Populate(mnuOpenRecent, @mnuOpenRecentClick);
  mnuOpenRecent.Enabled := FMRU.Count > 0;
End;

Procedure TfrmSyncedVideoPlayer.mnuOpenClick(Sender: TObject);
Begin
  If dlgOpen.Execute Then
  Begin
    OpenVideo(dlgOpen.Files);
  End;
End;

Procedure TfrmSyncedVideoPlayer.mnuOpenRecentClick(Sender: TObject);
Var
  slFiles: TStringList;
Begin
  If (Sender Is TMenuItem) And (TMenuItem(Sender).Tag < FMRU.Count) Then
  Begin
    slFiles := TStringList.Create;
    Try
      slFiles.Add(FMRU.Value(TMenuItem(Sender).Tag));
      OpenVideo(slFiles);
    Finally
      slFiles.Free;
    End;
  End;
End;

Procedure TfrmSyncedVideoPlayer.mnuToggleVideoClick(Sender: TObject);
Begin
  If (fmeSyncedVideo.VideoFileCount Mod 2) = 0 Then
  Begin
    If (Width > Height) Then
      fmeSyncedVideo.Layout(1, fmeSyncedVideo.VideoFileCount)
    Else
      fmeSyncedVideo.Layout(fmeSyncedVideo.VideoFileCount, 1);
  End
  Else If fmeSyncedVideo.VideoFileCount <> 1 Then
  Begin
    If (Width > Height) Then
      fmeSyncedVideo.Layout(2, 2, clsLeftToRightThenDown)
    Else
      fmeSyncedVideo.Layout(2, 2, clsTopToBottomThenRight);
  End;
End;

Procedure TfrmSyncedVideoPlayer.tmrUpdateTimer(Sender: TObject);
Begin
  tmrUpdate.Enabled := False;

  sbMain.Panels[1].Text := 'Start: ' + FormatDateTime('yyyy-mm-dd HH:nn',
    fmeSyncedVideo.StartDateTime);
  sbMain.Panels[2].Text := 'Duration: ' + FormatDateTime('HH:nn:ss',
    fmeSyncedVideo.DurationAsTime);
  sbMain.Panels[3].Text := 'End: ' + FormatDateTime('yyyy-mm-dd HH:nn',
    fmeSyncedVideo.EndDateTime);
End;

Procedure TfrmSyncedVideoPlayer.LoadGlobalSettings(oInifile: TIniFile);
Begin
  Inherited;

  FMRU.Load(oInifile, 'Files', 'MRU');
  fmeVideoPlayer.LoadSettings(oIniFile);
End;

Procedure TfrmSyncedVideoPlayer.SaveGlobalSettings(oInifile: TIniFile);
Begin
  Inherited;

  FMRU.Save(oInifile, 'Files', 'MRU');
  fmeVideoPlayer.SaveSettings(oIniFile);
End;

End.
